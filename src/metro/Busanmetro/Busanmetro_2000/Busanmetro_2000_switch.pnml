/*--------------------------------------------------------------------
스위치
--------------------------------------------------------------------*/
// 도색명
switch(FEAT_TRAINS, SELF, sw_Busanmetro_2000_cargo_subtype_text, cargo_subtype) {
    1: return string(STR_REFIT_LIVERY_Busanmetro_2000_WOOJIN);
    2: return string(STR_REFIT_LIVERY_Busanmetro_2000_DAWONSYS);
    return string(STR_REFIT_LIVERY_Busanmetro_2000_GTO);
}

// 편성그래픽
switch (FEAT_TRAINS, SELF, sw_Busanmetro_2000_6car, position_in_vehid_chain % 6) {
    1:set_Busanmetro_2000_wagon;
    2:set_Busanmetro_2000_panto_wagon;
    3:set_Busanmetro_2000_wagon;
    4:set_Busanmetro_2000_panto_wagon;
    5:set_Busanmetro_2000_engineT;
    set_Busanmetro_2000_engineH; //default
    }

switch (FEAT_TRAINS, SELF, sw_Busanmetro_2000_6car_r, position_in_vehid_chain % 6) {
    1:set_Busanmetro_2000_panto_wagon_r;
    2:set_Busanmetro_2000_wagon_r;
    3:set_Busanmetro_2000_panto_wagon_r;
    4:set_Busanmetro_2000_wagon_r;
    5:set_Busanmetro_2000_engineH_r;
    set_Busanmetro_2000_engineT_r; //default
    }

switch(FEAT_TRAINS, SELF, sw_Busanmetro_2000_6car_flipped, vehicle_is_flipped){
    0: sw_Busanmetro_2000_6car_r;
    sw_Busanmetro_2000_6car;
}

switch(FEAT_TRAINS, PARENT, sw_Busanmetro_2000_6car_reversed, vehicle_is_reversed){
    0: sw_Busanmetro_2000_6car;
    sw_Busanmetro_2000_6car_flipped;
}

// 편성량
switch (FEAT_TRAINS, SELF, sw_articulated_Busanmetro_2000_6car, extra_callback_info1) {
    1..5: Busanmetro_2000_6Car;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
    }

// 사운드
switch(FEAT_TRAINS, SELF, sw_Busanmetro_2000_GTO_sound, extra_callback_info1) {
    SOUND_EVENT_START: sound("sounds/ALSTOMGTOstart.wav");
    SOUND_EVENT_LOAD_UNLOAD: sound("sounds/BTC_DOOR_CLOSE.wav");
    return CB_RESULT_NO_SOUND;
    }

switch(FEAT_TRAINS, SELF, sw_Busanmetro_2000_WOOJIN_sound, extra_callback_info1) {
    SOUND_EVENT_START: sound("sounds/WOOJIN_IGBT_2start.wav");
    SOUND_EVENT_LOAD_UNLOAD: sound("sounds/BTC_DOOR_CLOSE.wav");
    return CB_RESULT_NO_SOUND;
    }

switch(FEAT_TRAINS, SELF, sw_Busanmetro_2000_DAWONSYS_sound, extra_callback_info1) {
    SOUND_EVENT_START: sound("sounds/DAWONSYSIGBTstart.wav");
    SOUND_EVENT_LOAD_UNLOAD: sound("sounds/BTC_DOOR_CLOSE.wav");
    return CB_RESULT_NO_SOUND;
    }
    
switch(FEAT_TRAINS, SELF, sw_Busanmetro_2000_sound, cargo_subtype) {
    1: sw_Busanmetro_2000_WOOJIN_sound;
    2: sw_Busanmetro_2000_DAWONSYS_sound;
    sw_Busanmetro_2000_GTO_sound;
}

// 속도변수
switch(FEAT_TRAINS, SELF, sw_Busanmetro_2000_speed, Use_max_virtual_speed) {
    1: return Metro_Speed_Train;
    return 100;
    }